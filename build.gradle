buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.android.support:support-v4:22.2.0'                         // support library (Drawers)
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.2'               // animated gif drawables
    compile 'com.android.support:appcompat-v7:22.2.0'                       // support library (material design)
    compile 'com.afollestad:material-dialogs:0.6.4.1'                       // material design for dialogs
//    compile 'org.jsoup:jsoup:1.8.1'
    compile 'org.mozilla:rhino:1.7.6'                                       // JS for cloudflare solving
    compile fileTree(dir: 'libs', include: '*.jar')                         // apache html entity + custom jsoup
}

android {
    compileSdkVersion 21
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 213
        versionName "2.1.3"
    }
    
    try {
        def proc = 'git rev-parse --short HEAD'.execute()
        def gitOutput = new StringBuilder('-')
        proc.waitForProcessOutput(gitOutput, System.err)
        def hash = gitOutput.toString().replace('\n', '').replace('\r', '')
        if(!hash.empty) {
            defaultConfig.versionName += hash
        }
    } catch (ignored) {
        println "Be sure that git executable is in your PATH to see extended version info in app"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file("/home/adonai/devel/keystore/Adonai")
            storePassword "release"
            keyAlias "adonai"
            keyPassword "release"
        }
    }

    buildTypes {
        pro {
            debuggable false
            signingConfig signingConfigs.release
            applicationIdSuffix '.pro'
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
        }
    }
}